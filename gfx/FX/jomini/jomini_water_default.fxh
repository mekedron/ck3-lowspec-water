# Better Water Without Advanced Shaders - modified copy of
# game/gfx/FX/jomini/jomini_water_default.fxh from CK3 1.19.0.6 (Scribe).
# Changes vs vanilla:
#   * new CalcWaterCheap( VS_OUTPUT_WATER ), appended after CalcWater( VS_OUTPUT_WATER ):
#     waves, sun highlight and sky reflection for the low spec ocean and lakes.
#     Nothing else in the file is changed.
Includes = {
	"better_water_options.fxh"
	"cw/heightmap.fxh"
	"cw/utility.fxh"
	"cw/camera.fxh"
	"jomini/jomini_fog.fxh"
	"jomini/jomini_lighting.fxh"
	"jomini/jomini_water.fxh"
	"jomini/map_lighting.fxh"
	"clouds.fxh"
}

Code
[[
	#ifndef JOMINIWATER_MapSize
		#define JOMINIWATER_MapSize MapSize
	#endif
	
	#ifndef JOMINIWATER_BorderLerpSize
		#define JOMINIWATER_BorderLerpSize 0.008
	#endif
]]

VertexShader =
{
	MainCode JominiWaterVertexShader
	{
		Input = "VS_INPUT_WATER"
		Output = "VS_OUTPUT_WATER"
		Code
		[[
			PDX_MAIN
			{
				VS_OUTPUT_WATER VertexOut;
				VertexOut.WorldSpacePos = float3( Input.Position.x, _WaterHeight, Input.Position.y );
				
				#ifdef JOMINIWATER_BORDER_LERP
					VertexOut.WorldSpacePos.x = JOMINIWATER_MapSize.x + Input.Position.x * JOMINIWATER_BorderLerpSize;
				#endif
				
				VertexOut.Position = FixProjectionAndMul( ViewProjectionMatrix, float4( VertexOut.WorldSpacePos.xyz, 1.0 ) );
				
				VertexOut.UV01 = float2( VertexOut.WorldSpacePos.x / JOMINIWATER_MapSize.x, 1.0 - VertexOut.WorldSpacePos.z / JOMINIWATER_MapSize.y );
				
				return VertexOut;
			}
		]]
	}
}

PixelShader =
{	
	TextureSampler WaterColorTexture
	{
		Ref = JominiWaterTexture0
		MagFilter = "Linear"
		MinFilter = "Linear"
		MipFilter = "Linear"
		SampleModeU = "Wrap"
		SampleModeV = "Wrap"
	}
	TextureSampler AmbientNormalTexture
	{
		Ref = JominiWaterTexture1
		MagFilter = "Linear"
		MinFilter = "Linear"
		MipFilter = "Linear"
		SampleModeU = "Wrap"
		SampleModeV = "Wrap"
	}
	TextureSampler FlowMapTexture
	{
		Ref = JominiWaterTexture2
		MagFilter = "Linear"
		MinFilter = "Linear"
		MipFilter = "Linear"
		SampleModeU = "Wrap"
		SampleModeV = "Wrap"
	}
	TextureSampler FlowNormalTexture
	{
		Ref = JominiWaterTexture3
		MagFilter = "Linear"
		MinFilter = "Linear"
		MipFilter = "Linear"
		SampleModeU = "Wrap"
		SampleModeV = "Wrap"
	}
	TextureSampler ReflectionCubeMap
	{
		Ref = JominiWaterTexture4
		MagFilter = "Linear"
		MinFilter = "Linear"
		MipFilter = "Linear"
		SampleModeU = "Wrap"
		SampleModeV = "Wrap"
		Type = "Cube"
	}
	TextureSampler FoamTexture
	{
		Ref = JominiWaterTexture5
		MagFilter = "Linear"
		MinFilter = "Linear"
		MipFilter = "Linear"
		SampleModeU = "Wrap"
		SampleModeV = "Wrap"
	}
	TextureSampler FoamRampTexture
	{
		Ref = JominiWaterTexture6
		MagFilter = "Linear"
		MinFilter = "Linear"
		MipFilter = "Linear"
		SampleModeU = "Clamp"
		SampleModeV = "Clamp"
	}
	TextureSampler FoamMapTexture
	{
		Ref = JominiWaterTexture7
		MagFilter = "Linear"
		MinFilter = "Linear"
		MipFilter = "Linear"
		SampleModeU = "Wrap"
		SampleModeV = "Wrap"
	}
	TextureSampler FoamNoiseTexture
	{
		Ref = JominiWaterTexture8
		MagFilter = "Linear"
		MinFilter = "Linear"
		MipFilter = "Linear"
		SampleModeU = "Wrap"
		SampleModeV = "Wrap"
	}
	
	TextureSampler RefractionTexture
	{
		Ref = JominiRefraction
		MagFilter = "Linear"
		MinFilter = "Linear"
		MipFilter = "Linear"
		SampleModeU = "Clamp"
		SampleModeV = "Clamp"
	}

	Code
	[[
		#ifndef JOMINIWATER_GlobalTime
			#define JOMINIWATER_GlobalTime GlobalTime
		#endif
		
		struct SWaterParameters
		{
			float4 _ScreenSpacePos;
			float3 _WorldSpacePos;
			float2 _WorldUV;
			float  _Depth;
			float3 _FlowNormal;
			float  _FlowFoamMask;
			float  _NoiseScale;
			float  _WaveSpeedScale;
			float  _WaveNoiseFlattenMult;
			#ifdef WATER_LOCAL_SPACE_NORMALS
			float3 _Tangent;
			float3 _Bitangent;
			float3 _Normal;
			#endif
		};
		
		struct SWaterOutput
		{
			float4  _Color;
			float	_Depth;
			float3	_Normal;
			float	_ReflectionAmount;
		};

		float CalcSingleWave( float3 WorldSpacePos, float Depth, float TimeOffset )
		{
			// Scale wave frequency to keep consistent width
			float WaveFrequency = 0.4f; // Wider waves in shallow water
			
			float WaveLines = ( 1.0f - Depth ) - GlobalTime * 0.15f + TimeOffset;
			WaveLines = frac( WaveLines * WaveFrequency );
			
			// return WaveLines;
			// Only create waves in a portion of the cycle (0.5 to 1.0), leaving gaps (0.0 to 0.5)

			// Remap to 0-1 range for the wave portion
			float WavePortion = ( WaveLines - 0.5f ) * 2.0f;
			
			// Sharp leading edge (front of wave toward shore) - at END of wave portion
			float SharpEdge = smoothstep( 0.8f, 1.0f, WavePortion );
			
			// Soft trailing edge - strongest near sharp edge, fades toward ocean
			float TrailingFade = ( smoothstep( 0.1, 0.8f, WavePortion ) ) * 0.4f;
			
			// Combine for asymmetric wave shape
			float WaveShape = max( SharpEdge, TrailingFade );
			
			WaveLines = WaveShape;
			
			// Add depth-based fade: opaque near shore, fade toward ocean
			float DepthFade = smoothstep( 3.0f, 1.0f, Depth );
			WaveLines *= DepthFade;
			
			return WaveLines;
		}
		
		float CalcApproachingWaves( float2 WorldUV, float3 WorldSpacePos, float Depth )
		{
			// Sample flow map including alpha channel
			float4 FlowData = PdxTex2D( FlowMapTexture, WorldUV );
			float ShoreWaveMask = FlowData.a; // Alpha channel controls where waves appear
			// Early out if no shore waves should appear 
			if ( ShoreWaveMask <= 0.0f )
			{
				return 0.0f;
			}
			
			// Create 3 wave layers with basic wave patterns
			float Wave1 = CalcSingleWave( WorldSpacePos, Depth, 0.0f );
			float Wave2 = Wave1;
			float Wave3 = CalcSingleWave( WorldSpacePos, Depth, 1.0f );
			
			// Large scale mask UV transforms - red channel (different for each wave)
			float2 BaseUV = WorldSpacePos.xz * 0.001f; // Large scale
			float2 LargeScaleMaskUV1A = BaseUV + GlobalTime * float2( 0.002f, 0.001f );
			float2 LargeScaleMaskUV2A = BaseUV * 1.3f + GlobalTime * float2( -0.0015f, 0.0025f );
			float2 LargeScaleMaskUV3A = BaseUV * 0.8f + GlobalTime * float2( 0.0018f, -0.002f );
			
			// Large scale mask UV transforms - blue channel (different scale and motion for variety)
			float2 BlueBaseUV = BaseUV; // Different base scale
			float2 LargeScaleMaskUV1B = BlueBaseUV * 0.7f + GlobalTime * float2(0.0015f, -0.001f);
			float2 LargeScaleMaskUV2B = BlueBaseUV * 1.8f + GlobalTime * float2(-0.0008f, 0.0032f);
			float2 LargeScaleMaskUV3B = BlueBaseUV * 1.1f + GlobalTime * float2(0.0015f, 0.0009f);
			
			// Sample large scale masks for each wave
			float LargeScaleMask1A = PdxTex2DUpscaleNative( FoamTexture, LargeScaleMaskUV1A ).g;
			float LargeScaleMask1B = PdxTex2DUpscaleNative( FoamTexture, LargeScaleMaskUV1B ).g;
			float LargeScaleMask2A = PdxTex2DUpscaleNative( FoamTexture, LargeScaleMaskUV2A ).g;
			float LargeScaleMask2B = PdxTex2DUpscaleNative( FoamTexture, LargeScaleMaskUV2B ).g;
			float LargeScaleMask3A = PdxTex2DUpscaleNative( FoamTexture, LargeScaleMaskUV3A ).g;
			float LargeScaleMask3B = PdxTex2DUpscaleNative( FoamTexture, LargeScaleMaskUV3B ).g;
			
			// Combine red and blue channels for each wave
			float LargeScaleMask1 = LargeScaleMask1A * LargeScaleMask1B;
			float LargeScaleMask2 = LargeScaleMask2A * LargeScaleMask2B;
			float LargeScaleMask3 = LargeScaleMask3A * LargeScaleMask3B;
			
			// Apply large scale masks to individual waves
			Wave1 *= LargeScaleMask1;
			Wave2 *= LargeScaleMask2;
			Wave3 *= LargeScaleMask3;
			
			// Recombine masked waves
			float ApproachingWaves = saturate( Wave1 + Wave2 + Wave3 );
			
			// Small-scale mask that affects all waves, with more opacity where waves are translucent 
			float2 SmallScaleMaskUV1 = WorldSpacePos.xz * 0.06f + GlobalTime * 0.2f * float2( 0.02f, 0.01f );
			float2 SmallScaleMaskUV2 = WorldSpacePos.xz * 0.5f + GlobalTime * float2( -0.08f, -0.06f );
			float SmallScaleMask1 = PdxTex2DUpscaleNative( FoamTexture, SmallScaleMaskUV1 ).b;
			float SmallScaleMask2 = PdxTex2DUpscaleNative( FoamNoiseTexture, SmallScaleMaskUV2 ).g;
			
			// Combine the two small scale masks with max for more complex patterns
			float SmallScaleMask = min(SmallScaleMask1, SmallScaleMask2);
			
			// Apply small scale mask with inverse relationship to wave opacity
			// Where waves are more translucent (lower ApproachingWaves), mask has more effect
			float MaskStrength = lerp( 0.3f, 1.0f, 1.0f - ApproachingWaves );
			SmallScaleMask = lerp( 1.0f, SmallScaleMask, MaskStrength );
			
			ApproachingWaves *= SmallScaleMask;
			
			// Apply foam map texture as a mask
			float FoamMap = PdxTex2D( FoamMapTexture, WorldUV ).g;
			ApproachingWaves *= FoamMap;

			return ApproachingWaves;
		}
		
		float CalcFoamFactor( float2 UV01, float2 WorldSpacePosXZ, float Depth, float FlowFoamMask, float3 FlowNormal )
		{
			float2 NoiseUV = WorldSpacePosXZ * _WaterFoamNoiseScale;
			float FoamNoise1 = PdxTex2DUpscaleNative( FoamNoiseTexture, NoiseUV + float2(1,1) * JOMINIWATER_GlobalTime * _WaterFoamNoiseSpeed ).r * 0.75;
			float FoamNoise2 = (PdxTex2DUpscaleNative( FoamNoiseTexture, NoiseUV * 3 + float2(1,-1) * JOMINIWATER_GlobalTime * _WaterFoamNoiseSpeed ).r - 0.5) * 0.5; // +/-0.25
			float FoamNoise3 = (PdxTex2DUpscaleNative( FoamNoiseTexture, NoiseUV * 5 + float2(-1,0) * JOMINIWATER_GlobalTime * _WaterFoamNoiseSpeed ).r - 0.5) * 0.25; // +/-0.125
			float FoamNoise = ( FoamNoise1 + FoamNoise2 + FoamNoise3 );

			float FoamMap = 1.0 - PdxTex2DUpscaleNative( FoamMapTexture, UV01 ).r;
			float FoamBase = pow( FoamMap, 2.0 ) * 2.375 - 1.0;
			
			float NoiseCeiling = 2.0;
			float FoamFactor = smoothstep( FoamBase, FoamBase + NoiseCeiling, 1.0 - FoamNoise );
			
			float FoamShoreMask = 1.0 - saturate( (_WaterFoamShoreMaskDepth - Depth) * _WaterFoamShoreMaskSharpness );
			
			FoamFactor *= _WaterFoamStrength * FoamShoreMask;
			
			float3 Foam = PdxTex2DUpscaleNative( FoamTexture, WorldSpacePosXZ * _WaterFoamScale + FlowNormal.xz * _WaterFoamDistortFactor ).rgb;
			float3 FoamRamp = PdxTex2DLod0( FoamRampTexture, float2( FoamFactor * FlowFoamMask, 0.5 ) ).rgb;
			
			FoamFactor = saturate( dot( Foam, FoamRamp ) );
			
			return FoamFactor;
		}
		
		float3 CalcTerrainUnderwaterSeeThrough( float Depth, float3 WorldSpacePos, float3 WaterColorMap, float3 Color )
		{
			float3 ToCameraDir = normalize( CameraPosition.xyz - WorldSpacePos );
			float WaterDistance = Depth / ToCameraDir.y;
			Color = lerp( WaterColorMap, Color, saturate( exp( -_WaterSeeThroughDensity * WaterDistance ) ) );
			
			float WaterSeeThroughShoreMask = 1.0 - saturate( ( _WaterSeeThroughShoreMaskDepth - Depth ) * _WaterSeeThroughShoreMaskSharpness );

			Color = lerp( Color, WaterColorMap, WaterSeeThroughShoreMask );
			
			return Color;
		}

		float3 CalcRefraction( float3 WorldSpacePos, float3 Normal, float2 ScreenPos, float3 WaterColor, float Depth )
		{
			float3 WaterColorMap = lerp( WaterColor, _WaterColorMapTint, _WaterColorMapTintFactor );
			
			#if defined( JOMINI_REFRACTION_ENABLED )
				float4 RefractionSample = PdxTex2DLod0( RefractionTexture, ScreenPos / _ScreenResolution );
				float3 RefractionWorldSpacePos = DecompressWorldSpace( WorldSpacePos, RefractionSample.a );
				float RefractionDepth = WorldSpacePos.y - RefractionWorldSpacePos.y;
				Depth = min( Depth, RefractionDepth );
				
				float RefractionShoreMask = 1.0 - saturate( ( _WaterRefractionShoreMaskDepth - Depth ) * _WaterRefractionShoreMaskSharpness );
				
				// Use 1080p as the normalizing factor for the refraction offset
				// Note, previous implementation had the refraction offset scaled by current resolution which made the effect stronger for lower resolutions and weaker for high resolutions, now the effect should stay consistent across resolutions
				float2 RefractionOffset = mul( ViewMatrix, float4( Normal.x, 0, Normal.z, 0 ) ).xy * float2( -1.0 / 1920.0, 1.0 / 1080.0 );
				RefractionOffset *= _WaterRefractionScale * RefractionShoreMask * _WaterRefractionFade;
			
				float4 OffsetRefractionSample = PdxTex2DLod0( RefractionTexture, ScreenPos / _ScreenResolution + RefractionOffset );
				float3 OffsetRefractionWorldSpacePos = DecompressWorldSpace( WorldSpacePos, OffsetRefractionSample.a );
				
				float OffsetStep = step( WorldSpacePos.y, OffsetRefractionWorldSpacePos.y );
				RefractionSample = lerp( OffsetRefractionSample, RefractionSample, OffsetStep );
				RefractionWorldSpacePos = lerp( OffsetRefractionWorldSpacePos, RefractionWorldSpacePos, OffsetStep );
				RefractionDepth = WorldSpacePos.y - RefractionWorldSpacePos.y;
				
				float2 RefractionWaterColorUV = float2( RefractionWorldSpacePos.x / JOMINIWATER_MapSize.x, 1.0 - RefractionWorldSpacePos.z / JOMINIWATER_MapSize.y );
				float3 RefractionWaterColorMap = PdxTex2D( WaterColorTexture, RefractionWaterColorUV ).rgb;
				RefractionWaterColorMap = lerp( RefractionWaterColorMap, _WaterColorMapTint, _WaterColorMapTintFactor );
			
				float3 Refraction = CalcTerrainUnderwaterSeeThrough( RefractionDepth, RefractionWorldSpacePos, RefractionWaterColorMap, RefractionSample.rgb );

				#if !defined( RIVER )
					Refraction = lerp( WaterColorMap, Refraction, pow( 1.0 - _WaterZoomedInZoomedOutFactor, 2.0 ) );
				#endif
			#else
				float3 Refraction = WaterColorMap;
			#endif
			
			return Refraction;
		}
		
		float3 CalcReflection( float3 Normal, float3 ToCameraDir, float CloudShadowMask )
		{
			float3 ReflectionNormal = Normal;
			ReflectionNormal.y += _WaterReflectionNormalFlatten; // TODO, decay with distance?
			ReflectionNormal = normalize( ReflectionNormal );
			float3 ReflectionVector = reflect( -ToCameraDir, ReflectionNormal );
			float3 Reflection = PdxTexCube( ReflectionCubeMap, ReflectionVector ).rgb * GetWaterCubemapIntensity( CloudShadowMask );
			
			return Reflection;
		}

		// This used to be the default lighting model, it has now been replaced
		// Moving it here because water is still using it and water shading is a bit special
		struct SWaterLightingProperties
		{
			float3 _WorldSpacePos;
			float3 _ToCameraDir;
			float3 _Normal;
			float3 _Diffuse;
		
			float3 _SpecularColor;
			float _Glossiness;
			float _NonLinearGlossiness;
		};
		
		#define PDX_GlossScale 11.0
		#define PDX_GlossBias 0.0
		#define PDX_MaxMipLevel 8.0
		float GetNonLinearGlossiness( float Glossiness )
		{
			return exp2( PDX_GlossScale * Glossiness + PDX_GlossBias );
		}
		
		float3 FresnelSchlick( float3 SpecularColor, float3 E, float3 H )
		{
			return SpecularColor + (vec3(1.0) - SpecularColor) * pow( 1.0 - saturate( dot(E, H) ), 5.0 );
		}
		
		SWaterLightingProperties GetLightingProperties( float3 WorldSpacePos, float3 Diffuse, float3 Normal, float4 Material )
		{
			float3 ToCameraDir = normalize( CameraPosition - WorldSpacePos );
			
			SWaterLightingProperties lightingProperties;
			lightingProperties._WorldSpacePos = WorldSpacePos;
			lightingProperties._ToCameraDir = ToCameraDir;
			lightingProperties._Normal = Normal;
			
			float SpecRemapped = Material.g * Material.g * 0.4;
			float Metalness = 1.0 - (1.0 - Material.b) * (1.0 - Material.b);
			float Glossiness = Material.a;
			lightingProperties._Diffuse = MetalnessToDiffuse( Metalness, Diffuse );
			lightingProperties._Glossiness = Glossiness;
			lightingProperties._SpecularColor = MetalnessToSpec( Metalness, Diffuse, SpecRemapped );
			lightingProperties._NonLinearGlossiness = GetNonLinearGlossiness( Glossiness );
			
			return lightingProperties;
		}
		
		float GetEnvmapMipLevel( float Glossiness )
		{
			return (1.0 - Glossiness) * (PDX_MaxMipLevel);
		}
		
		float3 FresnelGlossy( float3 SpecularColor, float3 E, float3 N, float Smoothness )
		{
			return SpecularColor + (max(vec3(Smoothness), SpecularColor) - SpecularColor) * pow(1.0 - saturate(dot(E, N)), 5.0);
		}
		
		float3 FresnelGlossy( SWaterLightingProperties Properties )
		{
			return FresnelGlossy( Properties._SpecularColor, Properties._ToCameraDir, Properties._Normal, Properties._Glossiness );
		}
		
		float3 GetReflectiveColor( SWaterLightingProperties Properties, PdxTextureSamplerCube EnvironmentMap, float EnvironmentMapIntensity, float4x4 EnvironmentMapRotation )
		{	
			float MipmapIndex = GetEnvmapMipLevel( Properties._Glossiness );
			float3 ReflectionVector = reflect( -Properties._ToCameraDir, Properties._Normal );
			float3 RotatedCubemapUV = mul( CastTo3x3( EnvironmentMapRotation ), ReflectionVector );
			float3 ReflectiveColor = PdxTexCubeLod( EnvironmentMap, RotatedCubemapUV, MipmapIndex ).rgb * EnvironmentMapIntensity;
			return ReflectiveColor * FresnelGlossy( Properties );
		}
		
		void ImprovedBlinnPhong( float3 LightColor, float3 ToLightDir, SWaterLightingProperties Properties, out float3 DiffuseLightOut, out float3 SpecularLightOut )
		{
			float3 H = normalize(Properties._ToCameraDir + ToLightDir);
			float NdotL = saturate(dot(Properties._Normal, ToLightDir));
			float NdotH = saturate(dot(Properties._Normal, H));
		
			float normalization = (Properties._NonLinearGlossiness + 2.0) / 8.0;
			float3 specColor = normalization * pow(NdotH, Properties._NonLinearGlossiness) * FresnelSchlick(Properties._SpecularColor, ToLightDir, H);
		
			DiffuseLightOut = LightColor * NdotL;
			SpecularLightOut = specColor * LightColor * NdotL;
		}
		
		void CalculateSunLight( SWaterLightingProperties Properties, float ShadowTerm, float3 ToSunDirection, float WaterSunIntensity, out float3 DiffuseLightOut, out float3 SpecularLightOut )
		{
			float3 sunIntensity = DefaultEnvironmentSunDiffuse * DefaultEnvironmentSunIntensity * ShadowTerm * WaterSunIntensity;
			ImprovedBlinnPhong( sunIntensity, ToSunDirection, Properties, DiffuseLightOut, SpecularLightOut );
		}
		
		float3 AmbientLight( float3 WorldNormal, float3 AmbientColors[6] )
		{
			// add more of bottom ambient below objects
			WorldNormal = normalize(WorldNormal - smoothstep(-0.6, 0.5, dot(WorldNormal, float3(0, -1, 0))) * float3(0, 0.9, 0));
		
			float3 Squared = WorldNormal * WorldNormal;
			int3 isNegative = int3(lessThan(WorldNormal, vec3(0.0)));
			float3 Color = Squared.x * AmbientColors[isNegative.x] + Squared.y * AmbientColors[isNegative.y+2] + Squared.z * AmbientColors[isNegative.z+4];
		
			return Color;
		}
		
		float3 AmbientLight( float3 WorldNormal, float ShadowTerm ) 
		{	
			float3 AmbientColors[6];
			ShadowTerm = smoothstep( SHADOW_AMBIENT_MIN_FACTOR, SHADOW_AMBIENT_MAX_FACTOR, ShadowTerm );
			AmbientColors[0] = lerp( ShadowAmbientPosX, AmbientPosX, ShadowTerm );
			AmbientColors[1] = lerp( ShadowAmbientNegX, AmbientNegX, ShadowTerm );
			AmbientColors[2] = lerp( ShadowAmbientPosY, AmbientPosY, ShadowTerm );
			AmbientColors[3] = lerp( ShadowAmbientNegY, AmbientNegY, ShadowTerm );
			AmbientColors[4] = lerp( ShadowAmbientPosZ, AmbientPosZ, ShadowTerm );
			AmbientColors[5] = lerp( ShadowAmbientNegZ, AmbientNegZ, ShadowTerm );
		
			return AmbientLight( WorldNormal, AmbientColors );
		}
		
		float3 ComposeLight( SWaterLightingProperties Properties, float3 AmbientLight, float3 DiffuseLight, float3 SpecularLight )
		{
			float3 diffuse = ((AmbientLight + DiffuseLight) * Properties._Diffuse);
			float3 specular = SpecularLight;
			
			return diffuse + specular;
		}
		
		float3 ComposeLight( SWaterLightingProperties Properties, float ShadowTerm, float3 ToSunDirection, float3 DiffuseLight, float3 SpecularLight )
		{
			float NdotL = saturate( dot(Properties._Normal, ToSunDirection) );
			float3 AmbientColor = AmbientLight( Properties._Normal, NdotL * ShadowTerm );
			return ComposeLight( Properties, AmbientColor, DiffuseLight, SpecularLight );
		}

		SWaterOutput CalcWater( in SWaterParameters Input, float ShadowTerm )
		{
			float4 WaterColorAndSpec = PdxTex2D( WaterColorTexture, Input._WorldUV );
			float GlossMap = WaterColorAndSpec.a;

			float3 ToCamera = CameraPosition.xyz - Input._WorldSpacePos;
			float3 ToCameraDir = normalize( ToCamera );

			// "Noise" normals
			float2 UVCoord = Input._WorldSpacePos.xz * float2( 1.0f , -1.0f ) * Input._NoiseScale;
			float3 NormalMap1 = SampleNormalMapTexture( AmbientNormalTexture, UVCoord, _WaterWave1Scale, _WaterWave1Rotation, JOMINIWATER_GlobalTime * _WaterWave1Speed * Input._WaveSpeedScale, _WaterWave1NormalFlatten * Input._WaveNoiseFlattenMult );
			float3 NormalMap2 = SampleNormalMapTexture( AmbientNormalTexture, UVCoord, _WaterWave2Scale, _WaterWave2Rotation, JOMINIWATER_GlobalTime * _WaterWave2Speed * Input._WaveSpeedScale, _WaterWave2NormalFlatten * Input._WaveNoiseFlattenMult );
			float3 NormalMap3 = SampleNormalMapTexture( AmbientNormalTexture, UVCoord, _WaterWave3Scale, _WaterWave3Rotation, JOMINIWATER_GlobalTime * _WaterWave3Speed * Input._WaveSpeedScale, _WaterWave3NormalFlatten * Input._WaveNoiseFlattenMult );
			
			float3 Normal = NormalMap1 + NormalMap2 + NormalMap3 + Input._FlowNormal;
			#ifdef WATER_LOCAL_SPACE_NORMALS
				float3x3 TBN = Create3x3( Input._Tangent, Input._Bitangent, Input._Normal );
				Normal = normalize( mul( Normal.xzy, TBN ) );
			#else
				Normal = normalize( Normal );
			#endif
			
			float FoamFactor = CalcFoamFactor( Input._WorldUV, Input._WorldSpacePos.xz, Input._Depth, Input._FlowFoamMask, Input._FlowNormal );
			
			float Facing = 1.0f - max( dot( Normal, ToCameraDir ), 0.0f );
			float3 WaterDiffuse = lerp( _WaterColorDeep, _WaterColorShallow, Facing );
			WaterDiffuse *= _WaterDiffuseMultiplier;
			
			// Get fog of war value for this position
			float FogOfWarAlphaValue = PdxTex2D( FogOfWarAlpha, 
				Input._WorldSpacePos.xz * WorldSpaceToTerrain0To1 ).r;

			// Get cloud shadow mask for this position
			float CloudShadowMask = GetCloudShadowMask( Input._WorldSpacePos.xz, FogOfWarAlphaValue );
			CloudShadowMask = max( 1.0f - ShadowTerm, CloudShadowMask );
			// Adjust glossiness based on fog of war
			float BaseGlossiness = lerp( _WaterGlossBase, GlossMap, 1 );
			float FowGlossiness = lerp( BaseGlossiness - 0.1f, BaseGlossiness, FogOfWarAlphaValue ); // make gloss lower in FoW

			SWaterLightingProperties lightingProperties;
			lightingProperties._WorldSpacePos = Input._WorldSpacePos;
			
			lightingProperties._ToCameraDir = ToCameraDir;
			lightingProperties._Normal = Normal;
			lightingProperties._Diffuse = WaterDiffuse + FoamFactor;
			lightingProperties._Glossiness = FowGlossiness;
			lightingProperties._SpecularColor = vec3(_WaterSpecular);
			lightingProperties._NonLinearGlossiness = GetNonLinearGlossiness( 
			lightingProperties._Glossiness ) * GetWaterGlossScale( CloudShadowMask );
			
			float3 DiffuseLight = vec3( 0.0f );
			float3 SpecularLight = vec3( 0.0f );
			float3 WaterToSunDir = GetWaterToSunDirection( CloudShadowMask );
			float WaterSunIntensity = GetWaterSunIntensity( CloudShadowMask );

			float SunIntensityMask = smoothstep( 0.05f, 0.1f, GlossMap ); // We use very dark values in the gloss map to simular occlusion
			CalculateSunLight( lightingProperties, 1.0f, WaterToSunDir, WaterSunIntensity * SunIntensityMask, DiffuseLight, SpecularLight );
			
			float3 FinalColor = ComposeLight( lightingProperties, 1.0f, WaterToSunDir, DiffuseLight, SpecularLight * GetWaterSpecularFactor( CloudShadowMask ) );

			float3 Refraction = CalcRefraction( Input._WorldSpacePos, Normal, Input._ScreenSpacePos.xy, WaterColorAndSpec.rgb, Input._Depth );

			float Depth = Input._Depth;
			#if defined( RIVER ) && defined( JOMINI_REFRACTION_ENABLED ) 
				float4 RefractionSample = PdxTex2DLod0( RefractionTexture, Input._ScreenSpacePos.xy / _ScreenResolution );
				float3 RefractionWorldSpacePos = DecompressWorldSpace( Input._WorldSpacePos, RefractionSample.a );
				float RefractionDepth = Input._WorldSpacePos.y - RefractionWorldSpacePos.y;
				Depth = min( Depth, RefractionDepth );
			#endif

			float WaterFade = 1.0f - saturate( ( _WaterFadeShoreMaskDepth - Depth ) * _WaterFadeShoreMaskSharpness );
			FinalColor *= WaterFade;

			float3 Reflection = CalcReflection( Normal, ToCameraDir, CloudShadowMask );
			
			float FresnelFactor = Fresnel( abs( dot( lightingProperties._ToCameraDir, Normal ) ), _WaterFresnelBias, _WaterFresnelPow ) * WaterFade;
			
			FinalColor += lerp( Refraction, Reflection, FresnelFactor );

			#ifdef JOMINIWATER_BORDER_LERP
				float ExtraFade = 1.0f - ( Input._WorldUV.x - 1.0f ) / JOMINIWATER_BorderLerpSize;
				WaterFade *= ExtraFade;
			#endif
			SWaterOutput Out;
			Out._Color = float4( FinalColor, WaterFade );
			Out._Depth = Input._Depth;
			Out._Normal = Normal;
			Out._ReflectionAmount = FresnelFactor;
			return Out;
		}

		SWaterOutput CalcWater( in SWaterParameters Input )
		{
			return CalcWater( Input, 1.0f );
		}

		SWaterOutput CalcWater( VS_OUTPUT_WATER Input, float ShadowTerm )
		{
			float2 HeightmapCoordinate = Input.WorldSpacePos.xz;
			#ifdef JOMINIWATER_BORDER_LERP
				HeightmapCoordinate.x -= JOMINIWATER_MapSize.x;
			#endif
			float Height = GetHeightMultisample( HeightmapCoordinate, 0.65f );
			SWaterParameters Params;
			Params._ScreenSpacePos = Input.Position;
			Params._WorldSpacePos = Input.WorldSpacePos;
			Params._WorldUV = Input.UV01;
			Params._Depth = Input.WorldSpacePos.y - Height;
			Params._NoiseScale = 0.05f;
			Params._WaveSpeedScale = 1.0f;
			Params._WaveNoiseFlattenMult = 1.0f;
			Params._FlowNormal = CalcFlow( FlowMapTexture, FlowNormalTexture, Params._WorldUV, Params._WorldSpacePos.xz, Params._FlowFoamMask );
			
			SWaterOutput Out = CalcWater( Params, ShadowTerm );

			// Calculate approaching waves
			float ApproachingWaves = CalcApproachingWaves( Input.UV01, Input.WorldSpacePos, Params._Depth );
			// Blend approaching waves with water color
			Out._Color.rgb = lerp( Out._Color.rgb, float3( 1.0f, 1.0f, 1.0f ), ApproachingWaves );
			return Out;
		}

		SWaterOutput CalcWater( VS_OUTPUT_WATER Input )
		{
			return CalcWater( Input, 1.0f );
		}

		// Better Water Without Advanced Shaders: the look of CalcWater with the
		// cheap parts only. Same wave layers 1 and 2, same sun model, same Fresnel blend
		// between the water colour map and the sky cube map, same shore fade - but no
		// flow maps, no foam, no approaching waves, no refraction, no third wave layer,
		// no shadow map, no clouds, and one height tap instead of the 9 tap multisample.
		// Sunny scenario constants, since the low spec map has no cloud mask.
		float4 CalcWaterCheap( VS_OUTPUT_WATER Input )
		{
			float2 HeightmapCoordinate = Input.WorldSpacePos.xz;
			#ifdef JOMINIWATER_BORDER_LERP
				HeightmapCoordinate.x -= JOMINIWATER_MapSize.x;
			#endif
			float Height = GetHeight( HeightmapCoordinate );
			float Depth = Input.WorldSpacePos.y - Height;
			float WaterFade = 1.0f - saturate( ( _WaterFadeShoreMaskDepth - Depth ) * _WaterFadeShoreMaskSharpness );

			float4 WaterColorAndSpec = PdxTex2D( WaterColorTexture, Input.UV01 );
			float GlossMap = WaterColorAndSpec.a;
			float3 ToCameraDir = normalize( CameraPosition.xyz - Input.WorldSpacePos );

			float2 UVCoord = Input.WorldSpacePos.xz * float2( 1.0f, -1.0f ) * 0.05f;
			float3 NormalMap1 = SampleNormalMapTexture( AmbientNormalTexture, UVCoord, _WaterWave1Scale, _WaterWave1Rotation, JOMINIWATER_GlobalTime * _WaterWave1Speed, _WaterWave1NormalFlatten );
			float3 NormalMap2 = SampleNormalMapTexture( AmbientNormalTexture, UVCoord, _WaterWave2Scale, _WaterWave2Rotation, JOMINIWATER_GlobalTime * _WaterWave2Speed, _WaterWave2NormalFlatten );
			float3 Normal = normalize( NormalMap1 + NormalMap2 );

			float Facing = 1.0f - max( dot( Normal, ToCameraDir ), 0.0f );
			float3 WaterDiffuse = lerp( _WaterColorDeep, _WaterColorShallow, Facing ) * _WaterDiffuseMultiplier;

			SWaterLightingProperties Props;
			Props._WorldSpacePos = Input.WorldSpacePos;
			Props._ToCameraDir = ToCameraDir;
			Props._Normal = Normal;
			Props._Diffuse = WaterDiffuse;
			Props._Glossiness = lerp( _WaterGlossBase, GlossMap, 1 );
			Props._SpecularColor = vec3( _WaterSpecular );
			Props._NonLinearGlossiness = GetNonLinearGlossiness( Props._Glossiness ) * WATER_SUNNY_GLOSS_SCALE;

			float3 DiffuseLight = vec3( 0.0f );
			float3 SpecularLight = vec3( 0.0f );
			float SunIntensityMask = smoothstep( 0.05f, 0.1f, GlossMap );
			CalculateSunLight( Props, 1.0f, ToWaterSunnySunDir, WATER_SUNNY_SUN_INTENSITY_MULTIPLIER * SunIntensityMask, DiffuseLight, SpecularLight );
			float3 FinalColor = ComposeLight( Props, 1.0f, ToWaterSunnySunDir, DiffuseLight, SpecularLight * WATER_SUNNY_SPECULAR_FACTOR );
			FinalColor *= WaterFade;

			float3 WaterColorMap = lerp( WaterColorAndSpec.rgb, _WaterColorMapTint, _WaterColorMapTintFactor );
			float3 Reflection = CalcReflection( Normal, ToCameraDir, 0.0f );
			float FresnelFactor = Fresnel( abs( dot( ToCameraDir, Normal ) ), _WaterFresnelBias, _WaterFresnelPow ) * WaterFade;
			FinalColor += lerp( WaterColorMap, Reflection, FresnelFactor );

			#ifdef JOMINIWATER_BORDER_LERP
				float ExtraFade = 1.0f - ( Input.UV01.x - 1.0f ) / JOMINIWATER_BorderLerpSize;
				WaterFade *= ExtraFade;
			#endif
			return float4( FinalColor, WaterFade );
		}
	]]
	
	MainCode JominiWaterPixelShader
	{
		Input = "VS_OUTPUT_WATER"
		Output = "PDX_COLOR"
		Code
		[[			
			PDX_MAIN
			{
				float4 Water = CalcWater( Input )._Color; 
				Water.rgb = ApplyMapDistanceFogWithoutFoW( Water.rgb, Input.WorldSpacePos );
				return Water;
			}
		]]
	}
}


BlendState BlendState
{
	BlendEnable = yes
	SourceBlend = "src_alpha"
	DestBlend = "inv_src_alpha"
	WriteMask = "RED|GREEN|BLUE"
}

RasterizerState RasterizerState
{
	DepthBias = -100
}
